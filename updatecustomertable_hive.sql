CREATE DATABASE IF NOT EXISTS CDW_SAPP; -- Only gets created the first time the script runs
DROP TABLE IF EXISTS CDW_SAPP.TEMP_CDW_SAPP_D_CUSTOMER;
CREATE EXTERNAL TABLE IF NOT EXISTS CDW_SAPP.TEMP_CDW_SAPP_D_CUSTOMER( -- Only gets created the first time the script runs
	CUST_SSN INT,
	CUST_F_NAME VARCHAR(40),
	CUST_M_NAME VARCHAR(40),
	CUST_L_NAME VARCHAR(40),
	CUST_CC_NO STRING,
	CUST_STREET VARCHAR(38),
	CUST_CITY VARCHAR(30),
	CUST_STATE VARCHAR(30),
	CUST_COUNTRY VARCHAR(30),
	CUST_ZIP VARCHAR(5),
	CUST_PHONE VARCHAR(8),
	CUST_EMAIL VARCHAR(40),
	LAST_UPDATED TIMESTAMP
)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
LOCATION "/Credit_Card_System/CDW_SAPP_D_CUSTOMER/";

CREATE TABLE IF NOT EXISTS CDW_SAPP.CDW_SAPP_D_CUSTOMER ( -- Only gets created the first time the script runs  
	CUST_SSN INT,
	CUST_F_NAME VARCHAR(40),
	CUST_M_NAME VARCHAR(40),
	CUST_L_NAME VARCHAR(40),
	CUST_CC_NO STRING,
	CUST_STREET VARCHAR(38),
	CUST_CITY VARCHAR(30),
	CUST_COUNTRY VARCHAR(30),
	CUST_ZIP VARCHAR(5),
	CUST_PHONE VARCHAR(8),
	CUST_EMAIL VARCHAR(40),
	LAST_UPDATED TIMESTAMP
)
PARTITIONED BY (CUST_STATE VARCHAR(30))
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
STORED AS TEXTFILE;

SET hive.exec.dynamic.partition = true;
SET hive.exec.dynamic.partition.mode = nonstrict;
INSERT INTO TABLE CDW_SAPP.CDW_SAPP_D_CUSTOMER PARTITION (CUST_STATE)

SELECT
	CUST_SSN,
	CUST_F_NAME,
	CUST_M_NAME,
	CUST_L_NAME,
	CUST_CC_NO,
	CUST_STREET,
	CUST_CITY,
	CUST_COUNTRY,
	CUST_ZIP,
	CUST_PHONE,
	CUST_EMAIL,
	LAST_UPDATED,
	CUST_STATE
FROM 
	CDW_SAPP.TEMP_CDW_SAPP_D_CUSTOMER;
	DROP TABLE CDW_SAPP.TEMP_CDW_SAPP_D_CUSTOMER;