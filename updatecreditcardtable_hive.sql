CREATE DATABASE IF NOT EXISTS CDW_SAPP; -- Only gets created the first time the script runs 
DROP TABLE IF EXISTS CDW_SAPP.TEMP_CDW_SAPP_F_CREDIT_CARD;
CREATE EXTERNAL TABLE IF NOT EXISTS CDW_SAPP.TEMP_CDW_SAPP_F_CREDIT_CARD( -- Only gets created the first time the script runs
CUST_CC_NO STRING, 
TIMEID VARCHAR(8), 
CUST_SSN INT,
BRANCH_CODE INT,
TRANSACTION_TYPE VARCHAR(30), 
TRANSACTION_VALUE DECIMAL(20,3), 
TRANSACTION_ID INT, 
LAST_UPDATED TIMESTAMP
)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
LOCATION "/Credit_Card_System/CDW_SAPP_F_CREDIT_CARD/";

CREATE TABLE IF NOT EXISTS CDW_SAPP.CDW_SAPP_F_CREDIT_CARD ( -- Only gets created the first time the script runs
CUST_CC_NO STRING, 
TIMEID VARCHAR(8),
CUST_SSN INT,
TRANSACTION_TYPE VARCHAR(30), 
TRANSACTION_VALUE DECIMAL(20,3), 
TRANSACTION_ID INT, 
LAST_UPDATED TIMESTAMP
)
PARTITIONED BY (BRANCH_CODE INT)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
STORED AS TEXTFILE;

SET hive.exec.dynamic.partition = true;
SET hive.exec.dynamic.partition.mode = nonstrict;

INSERT INTO TABLE CDW_SAPP.CDW_SAPP_F_CREDIT_CARD  PARTITION (BRANCH_CODE)

SELECT 
	CUST_CC_NO,
	TIMEID,
	CUST_SSN,
	TRANSACTION_TYPE,
	TRANSACTION_VALUE,
	TRANSACTION_ID,
	LAST_UPDATED,
	BRANCH_CODE
FROM CDW_SAPP.TEMP_CDW_SAPP_F_CREDIT_CARD;
DROP TABLE CDW_SAPP.TEMP_CDW_SAPP_F_CREDIT_CARD;
